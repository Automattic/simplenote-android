buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url "http://simperium.github.io/simplenote-android" }
    maven { url "http://wordpress-mobile.github.io/Android-PasscodeLock"}
    maven { url 'http://wordpress-mobile.github.io/WordPress-Android' }
}

dependencies {
    compile "com.simperium.android:simperium:0.6.3"
    compile "com.android.support:appcompat-v7:22.0.+"
    compile "com.google.android.gms:play-services:6.1.+"
    compile "org.wordpress:android-passcodelock:0.0.6"

    debugWearApp project(path:':Wear', configuration: 'debug')
    releaseWearApp project(path:':Wear', configuration: 'release')
}

version "1.3.2"

android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.automattic.simplenote"
        versionName project.version
        versionCode 41
        minSdkVersion 15
        targetSdkVersion 22

        buildConfigField "String", "SIMPERIUM_APP_ID", "\"${project.simperiumAppId}\""
        buildConfigField "String", "SIMPERIUM_APP_KEY", "\"${project.simperiumAppKey}\""
        buildConfigField "String", "HOCKEY_APP_ID", "\"${project.hockeyAppId}\"";
        buildConfigField "String", "GOOGLE_ANALYTICS_ID", "\"${project.googleAnalyticsId}\"";
        buildConfigField "String", "BUILD", "\"${gitDescribe()}\"";
        buildConfigField "String", "BUILD_HASH", "\"${gitHash()}\"";

     }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFile 'proguard.cfg'
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }
	
	lintOptions {
	    checkReleaseBuilds false
	}

}

if(["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0 ){
    android {
      signingConfigs {
          release {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
          debug {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
      }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

