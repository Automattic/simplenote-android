buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:0.6.+"
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
    maven { url "http://simperium.github.io/simplenote-android" }
    maven { url "http://simperium.github.io/simperium-android" }
    maven { url "http://wordpress-mobile.github.io/Android-PasscodeLock"}
}

dependencies {
    releaseCompile "com.simperium:simperium-android:0.4.0-alpha2"
    debugCompile "com.simperium:simperium-android-support:0.4.0-alpha2"
    compile "com.android.support:support-v4:18.0.+"
    compile "com.google:google-analytics:2.0-beta-4"
    compile "org.wordpress:android-passcodelock:0.0.3"
}

version "1.2"

android {

    compileSdkVersion 19
    buildToolsVersion "19"

    defaultConfig {
        packageName "com.automattic.simplenote"
        versionName project.version
        versionCode 20
        minSdkVersion 15
        targetSdkVersion 19
     }

}

// Generates the Config.java class using properties defined in gradle.properties
// or by other means provided by Gradle
task simplenoteConfig(group: "build", description: "Generate Config class") {
    def dir = "${buildDir}/source/simplenote"
    def file = new File(dir, "com/automattic/simplenote/Config.java")
    android {
        sourceSets {
            main {
                java {
                    srcDir dir
                }
            }
        }
    }
    outputs.file file
    doLast {
        file.getParentFile().mkdirs()
        def writer = new FileWriter(file)
        writer.write("""/* auto-generated file do not modify */
package com.automattic.simplenote;

public final class Config {

    public static final String simperium_app_id = "${simperiumAppId}";
    public static final String simperium_app_key = "${simperiumAppKey}";
    public static final String hockey_app_id = "${hockeyAppId}";

}
""")
        writer.close()
    }
}

// Add the simplenoteConfig task to every flavor's compile tasks
android.buildTypes.all { buildType ->
    def name = "compile${buildType.name.capitalize()}"
    tasks.whenTaskAdded { task ->
        if (task.name == name) {
            task.dependsOn(simplenoteConfig)
            task.mustRunAfter(simplenoteConfig)
        }
    }
}
if(["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0 ){
    android {
      signingConfigs {
          release {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
      }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

