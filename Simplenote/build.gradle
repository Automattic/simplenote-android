buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url "http://simperium.github.io/simplenote-android" }
    maven { url "http://simperium.github.io/simperium-android" }
    maven { url "http://wordpress-mobile.github.io/Android-PasscodeLock"}
}

dependencies {
    compile 'com.simperium.android:simperium:0.5.0-alpha'
    debugCompile 'com.simperium:simperium-test-util:0.5.0-alpha'
    compile "com.android.support:support-v4:20.+"
    compile "com.google:google-analytics:2.0-beta-4"
    compile "org.wordpress:android-passcodelock:0.0.5"
}

version "1.1.9"

android {

    compileSdkVersion 20
    buildToolsVersion "20"

    defaultConfig {
        applicationId "com.automattic.simplenote"
        versionName project.version
        versionCode 33
        minSdkVersion 15
        targetSdkVersion 20

        buildConfigField "String", "SIMPERIUM_APP_ID", "\"${project.simperiumAppId}\""
        buildConfigField "String", "SIMPERIUM_APP_KEY", "\"${project.simperiumAppKey}\""
        buildConfigField "String", "HOCKEY_APP_ID", "\"${project.hockeyAppId}\"";
        buildConfigField "String", "BUILD", "\"${gitDescribe()}\"";
        buildConfigField "String", "BUILD_HASH", "\"${gitHash()}\"";

     }

    buildTypes {
        release {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }
	
	lintOptions {
	    checkReleaseBuilds false
	}

}

if(["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0 ){
    android {
      signingConfigs {
          release {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
          debug {
              storeFile = file(project.storeFile)
              storePassword = project.storePassword
              keyAlias = project.keyAlias
              keyPassword = project.keyPassword
          }
      }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}

