buildscript {
    repositories {
        google()
        jcenter()
    }
}
apply plugin: 'com.android.application'

repositories {
    maven { url "https://maven.google.com" }
    jcenter()
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.automattic.simplenote"
        minSdkVersion 20
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly 'com.google.android.wearable:wearable:2.3.0'

    implementation 'com.google.android.support:wearable:2.3.0'
    implementation ('com.google.android.gms:play-services-wearable:16.0.1'){
        exclude group: 'com.android.support', module: 'support-v4'
    }
}

if(["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0 ){
    android {
        signingConfigs {
            release {
                storeFile = file(project.storeFile.replaceFirst("^~", System.getProperty("user.home")))
                storePassword = project.storePassword
                keyAlias = project.keyAlias
                keyPassword = project.keyPassword
            }
            debug {
                storeFile = file(project.storeFile.replaceFirst("^~", System.getProperty("user.home")))
                storePassword = project.storePassword
                keyAlias = project.keyAlias
                keyPassword = project.keyPassword
            }
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
